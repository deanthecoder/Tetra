#
# "Shield" visual effect originally by Xor (X.com/XorDev)
# Source: https://www.shadertoy.com/view/cltfRf
# See also: https://x.com/XorDev/status/1730436700000649470
#
    call main
    halt

main:
    ld $I, $fragCoord
    ld $o, 0.0, 0.0, 0.0, 0.0
    
    # for(i = 0.0; i < 1.0; ...)
    ld $i, 0.0
loop:
    jmp_ge $i, 1.0, end
    
    # p=(I+I-v)/v.y*i;
    ld $v, $iResolution
    ld $p, $I
    add $p, $I
    sub $p, $v
    div $p, $v[1]
    mul $p, $i
    
    # float z = max(1.-dot(p,p),0.);
    ld $z, 1.0
    ld $arg0, $p
    call dot
    sub $z, $retval
    max $z, 0.0
    
    # p/=.2+sqrt(z)*.3;
    sqrt $_t, $z
    mul $_t, 0.3
    add $_t, 0.2
    div $p, $_t
   
    # p.y+=fract(ceil(p.x=p.x/.9+iTime)*.5)+iTime*.2;
    ld $_t, $p[0]
    div $_t, 0.9
    add $_t, $iTime
    ld $p[0], $_t
    ceil $_t, $_t
    mul $_t, 0.5
    fract $_t, $_t
    add $p[1], $_t
    ld $_t, $iTime
    mul $_t, 0.2
    add $p[1], $_t
    
    # v=abs(fract(p)-.5);
    fract $_t, $p
    sub $_t, 0.5
    abs $v, $_t
    
    # O+=vec4(2,3,5,1)/2e3*z/(abs(max(v.x*1.5+v,v+v).y-1.)+.1-i*.09);
    ld $_t2, $v[0]
    mul $_t2, 1.5
    add $_t2, $v
    ld $_t, $v
    add $_t, $v
    max $_t, $_t2
    ld $_t, $_t[1]
    dec $_t
    abs $_t, $_t
    add $_t, 0.1
    ld $_t2, $i
    mul $_t2, 0.09
    sub $_t, $_t2
        
    ld $_t2, 2.0, 3.0, 5.0, 1.0
    div $_t2, 2000.0
    mul $_t2, $z
    div $_t2, $_t
    
    add $o, $_t2

    # for ... i += 0.1
    add $i, 0.01
    jmp loop
end:
    # return tanh(O*O);
    mul $o, $o
    tanh $o, $o
    ret $o
   
# dot(vec2 arg0): float
dot:
    ld $d, $arg0    
    mul $d, $d
    ld $s, $d[0]
    add $s, $d[1]
    ret $s
